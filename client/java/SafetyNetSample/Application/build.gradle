apply plugin: 'com.android.application'

dependencies {
    implementation "androidx.cardview:cardview:$cardview_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.gms:play-services-safetynet:$play_services_safetynet_version"

    implementation 'com.squareup.retrofit2:retrofit:2.7.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.2'
    implementation 'com.squareup.okhttp3:okhttp:3.14.7'
}

// The sample build uses multiple directories to
// keep boilerplate and common code separate from
// the main sample code.
List<String> dirs = [
        'main',     // main sample code; look here for the interesting stuff.
        'common',   // components that are reused by multiple samples
        'template'] // boilerplate code that is generated by the sample template process

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.example.android.safetynetsample"
        minSdkVersion 15
        targetSdkVersion 30
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            dirs.each { dir ->
                java.srcDirs "src/${dir}/java"
                res.srcDirs "src/${dir}/res"
            }
        }
        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/src']

    }

    defaultConfig {
        def props = new Properties()

        def fileInputStream = new FileInputStream(file('../local.properties'))
        props.load(fileInputStream)
        fileInputStream.close()

        buildConfigField('String', 'API_KEY', "\""+props.getProperty('safetynet_api_key')+"\"")
    }


}
